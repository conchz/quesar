plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

version = '1.0-SNAPSHOT'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'
mainClassName = 'io.vertx.core.Launcher'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

def vertxVersion = '3.4.2'
def mainVerticleName = 'io.quesar.starter.BootstrapVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-redis-client:$vertxVersion"
    compile "io.vertx:vertx-rx-java:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"
    compile 'org.msgpack:msgpack-core:0.8.13'
    compile 'ch.qos.logback:logback-classic:1.1.11'
    compile 'com.typesafe:config:1.3.1'


    testCompile "junit:junit:4.12"
    testCompile "io.vertx:vertx-unit:$vertxVersion"
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': "$mainClassName",
            'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

applicationDefaultJvmArgs = ["-Dvertx.logger-delegate-factory-class-name=io.vertx.core.logging.SLF4JLogDelegateFactory"]

run {
    args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}
